#@TYPE: Machine
#@NAME: hihope-rzg2m machine
#@DESCRIPTION: Hoperun Technology HiHope RZ/G2M platform (hihope-rzg2m)

SOC_FAMILY = "r8a774a1"

DEFAULTTUNE ?= "cortexa57-cortexa53"
TUNE_CONF ?= "conf/machine/include/tune-cortexa57-cortexa53.inc"
require conf/machine/include/${SOC_FAMILY}.inc
require ${TUNE_CONF}
require include/ecc-control.inc

# 32BIT package install (default is disable)
# This variables can be used only in multilib.
USE_32BIT_PKGS ?= "0"
USE_32BIT_WAYLAND ?= "0"
USE_32BIT_MMP ?= "0"

MACHINE_FEATURES = ""

KERNEL_IMAGETYPE = "Image"
IMAGE_FSTYPES_append = " tar.bz2 ext4"

SERIAL_CONSOLE = "115200 ttySC0"

# Configuration for kernel
PREFERRED_PROVIDER_virtual/kernel = "linux-renesas"
# Device tree for RZG2M
KERNEL_DEVICETREE = " \
    ${@base_ifelse("${ECC_FULL}" == "2"," "," \
    renesas/r8a774a1-hihope-rzg2m.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-idk-1110wr.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-mipi-2.1.dtb \
    renesas/r8a774a1-hihope-rzg2m-rev2.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-rev2.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-rev2-idk-1110wr.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-rev2-mipi-2.1.dtb \
    ")} \
    renesas/r8a774a3-hihope-rzg2m.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex-idk-1110wr.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex-mipi-2.1.dtb \
"

# Configuration for ARM Trusted Firmware
EXTRA_IMAGEDEPENDS += " arm-trusted-firmware optee-os"

# u-boot
PREFERRED_VERSION_u-boot = "v2018.09%"
EXTRA_IMAGEDEPENDS += " u-boot"
# G2M u-boot configure
UBOOT_CONFIG ??= "hihope-rzg2m"
UBOOT_CONFIG[hihope-rzg2m] = "r8a774a1_hihope-rzg2m_defconfig"

# flash writer
EXTRA_IMAGEDEPENDS += " flash-writer"

# libdrm
PREFERRED_VERSION_libdrm = "2.4.85"

# Add variable to Build Configuration in build log
BUILDCFG_VARS_append = " SOC_FAMILY"

# Command using to build kernel-module-gles
HOSTTOOLS += "sync"
